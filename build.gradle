plugins {
    // Apply the application plugin to add support for building a CLI application in Java.
    id 'application'
    id 'io.freefair.lombok' version '8.10.2'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'maven-publish'
    id 'com.gradleup.shadow' version '8.3.3'
}

ext {
    ociImageBase = 'local/' + rootProject.name
    ociImageName = ociImageBase + ':dev'
    ociTeeImageName = ociImageBase + '-unlocked:dev'
    // versions
    testContainersVersion = '1.20.4'
    springBootVersion = '3.3.8'
    systemStubsVersion = '2.0.1'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
    version += '-NEXT-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://docker-regis-adm.iex.ec/repository/maven-public/"
        credentials {
            username nexusUser
            password nexusPassword
        }
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")

    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"

    // This dependency is used by the application.
    implementation 'com.google.guava:guava:27.1-jre'

    // Dropbox
    implementation 'com.dropbox.core:dropbox-core-sdk:3.1.1'

    // Apache Commons-lang3
    implementation 'org.apache.commons:commons-lang3'

    // Spring Test
    implementation "org.springframework:spring-test"

}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('-parameters')
}

tasks.withType(Tar).configureEach {
    enabled = false
}

testing {
    suites {
        configureEach {
            useJUnitJupiter()
            dependencies {
                implementation platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion")
                implementation "org.mockito:mockito-junit-jupiter"
                implementation "uk.org.webcompere:system-stubs-core:$systemStubsVersion"    // activates env var setting
                implementation "uk.org.webcompere:system-stubs-jupiter:$systemStubsVersion" // activates env var setting
            }
        }
        itest(JvmTestSuite) {
            dependencies {
                implementation project()
                implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
                implementation 'com.github.tomakehurst:wiremock-jre8:2.28.1'
                implementation 'org.awaitility:awaitility'
                implementation "org.testcontainers:testcontainers:$testContainersVersion"
                implementation "org.testcontainers:junit-jupiter:$testContainersVersion"
            }
        }
    }
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.sonarqube.dependsOn tasks.jacocoTestReport

//gradle run
application {
    // Define the main class for the application.
    mainClass.set('com.iexec.worker.compute.post.Main')
}

// Jar with missing dependencies (app.jar)
jar {
    manifest {
        attributes 'Main-Class': 'com.iexec.worker.compute.post.Main'
    }
}

// Jar with all dependencies (app-all.jar)
shadowJar {
    archiveBaseName.set('app')
    archiveVersion.set('') // remove version from name
}

ext.jarPathForOCI = relativePath(tasks.shadowJar.outputs.files.singleFile)

tasks.register('buildImage', Exec) {
    group 'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn shadowJar
    commandLine 'docker', 'build', '--build-arg', 'jar=' + jarPathForOCI, '-t', ociImageName, '.'
}

tasks.register('buildSconeImage', Exec) {
    dependsOn buildImage
    environment 'IMG_FROM', ociImageName
    environment 'IMG_TO', ociTeeImageName
    commandLine 'docker/sconify.sh'
}
